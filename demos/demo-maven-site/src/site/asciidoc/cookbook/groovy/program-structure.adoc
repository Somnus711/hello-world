= 程序结构
:groovyITGuideDocBaseUrl: http://docs.groovy-lang.org/{groovyVersion}/html/documentation/guide-integrating.html

本章介绍Groovy编程语言的程序结构。

== 包名称

包名称与Java中的角色完全相同。它们允许我们在没有任何冲突的情况下分离代码库。Groovy类必须在类定义之前指定它们的包，否则假定是默认包。

定义包与Java非常相似：

[source,groovy]
----
// 定义名为com.yoursite的包
package com.yoursite
----

要在 `com.yoursite.com` 包中引用某个类 `Foo`，你需要使用完全限定名 `com.yoursite.com.Foo`，否则你可以使用 `import` 语句，如下所示。

== 导入

要引用任何类，你需要对其包的限定引用。Groovy遵循Java中允许 `import` 语句来解析类引用的概念。

例如，Groovy提供了几个构建器类，例如 `MarkupBuilder`。`MarkupBuilder` 在包 `groovy.xml` 中，所以为了使用这个类，你需要 `import` 它，如下所示：

[source,groovy]
----
// 导入类MarkupBuilder
import groovy.xml.MarkupBuilder

// 使用导入的类创建对象
def xml = new MarkupBuilder()

assert xml != null
----

=== 默认导入

默认导入是Groovy语言默认提供的导入。例如，查看以下代码：

[source,groovy]
----
new Date()
----

Java中的相同代码需要一个 `Date` 类的import语句，如下所示：import java.util.Date。默认情况下，Groovy会为你导入这些类。

以下导入由groovy为你默认添加：

[source,groovy]
----
import java.lang.*
import java.util.*
import java.io.*
import java.net.*
import groovy.lang.*
import groovy.util.*
import java.math.BigInteger
import java.math.BigDecimal
----

这样做是因为这些包中的类是最常用的。通过导入可以减少这些样板代码。

=== 简单的导入

一个简单的导入是一个import语句，你可以在其中完全定义类名和包。例如，下面代码中的import语句import groovy.xml.MarkupBuilder是一个简单的导入，它直接引用包内的类。

[source,groovy]
----
// 导入类MarkupBuilder
import groovy.xml.MarkupBuilder

// 使用导入的类创建对象
def xml = new MarkupBuilder()

assert xml != null
----

=== 星号导入

像Java一样，Groovy提供了一种特殊的方法，可以使用 `{asterisk}` 来导入包中的所有类，即所谓的星号导入。MarkupBuilder是一个包在groovy.xml包中的类，以及另一个名为StreamingMarkupBuilder的类。如果你需要使用这两个类，你可以：

[source,groovy]
----
import groovy.xml.MarkupBuilder
import groovy.xml.StreamingMarkupBuilder

def markupBuilder = new MarkupBuilder()

assert markupBuilder != null

assert new StreamingMarkupBuilder() != null
----

这是完全有效的代码。但是使用 `{asterisk}` 导入，我们只用一行就能达到同样的效果。星号导入包 `groovy.xml` 下的所有类：

[source,groovy]
----
import groovy.xml.*

def markupBuilder = new MarkupBuilder()

assert markupBuilder != null

assert new StreamingMarkupBuilder() != null
----

`{asterisk}` 导入的一个问题是它们可能会混乱你的本地命名空间。但是由于Groovy提供了各种别名，这很容易解决。

=== 静态导入

Groovy的静态导入功能允许你引用导入的类，就像它们是你自己的类中的静态方法一样：

[source,groovy]
----
import static Boolean.FALSE

assert !FALSE // 直接使用，没有Boolean前缀！
----

这类似于Java的静态导入功能，但它比Java更具动态性，因为它允许你定义与导入方法同名的方法，只要你有不同的类型：

[source,groovy]
----
import static java.lang.String.format // <1>

class SomeClass {

    String format(Integer i) { // <2>
        i.toString()
    }

    static void main(String[] args) {
        assert format('String') == 'String' // <3>
        assert new SomeClass().format(Integer.valueOf(1)) == '1'
    }
}
----
<1> 静态导入方法
<2> 与上面静态导入的方法同名的方法声明，但具有不同的参数类型
<3> 在java中编译错误，但在groovy中是有效的代码

如果你具有相同的类型，则导入的类优先。

=== 静态导入别名

使用 `as` 关键字的静态导入为命名空间问题提供了一个优雅的解。假设你想使用其 `getInstance()` 方法获取 `Calendar` 实例。这是一个静态方法，所以我们可以使用静态导入。但是不是每次都调用 `getInstance()`，这可能会在与类名分离时产生误导，我们可以使用别名导入它，以提高代码可读性：

[source,groovy]
----
import static Calendar.getInstance as now

assert now().class == Calendar.getInstance().class
----

=== 静态星号导入

静态星号导入与常规星号导入非常相似。它将导入给定类的所有静态方法。

例如，假设我们需要为我们的应用程序计算正弦和余弦。java.lang.Math类有一个名为 `sin` 和 `cos` 的静态方法，它们符合我们的需要。借助静态星号导入，我们可以：

[source,groovy]
----
import static java.lang.Math.*

assert sin(0) == 0.0
assert cos(0) == 1.0
----

如你所见，我们能够直接访问 `sin` 和 `cos` 方法，而无需 `Math.` 前缀。

=== 导入别名

使用类型别名，我们可以使用我们选择的名称来引用完全限定的类名。这可以使用 `as` 关键字完成，如前所述。

例如，我们可以将 `java.sql.Date` 作为 `SQLDate` 导入，并在与 `java.util.Date` 相同的文件中使用它，而不必使用任一类的完全限定名称：

[source,groovy]
----
import java.util.Date
import java.sql.Date as SQLDate

Date utilDate = new Date(1000L)
SQLDate sqlDate = new SQLDate(1000L)

assert utilDate instanceof java.util.Date
assert sqlDate instanceof java.sql.Date
----

== 脚本VS类

=== public static void main vs script

Groovy支持脚本和类。以下面的代码为例：

.Main.groovy
[source,groovy]
----
class Main {                                    // <1>
    static void main(String... args) {          // <2>
        println 'Groovy world!'                 // <3>
    }
}
----
<1> 定义一个 `Main` 类，名称是任意的
<2> `public static void main(String[])` 方法可用作类的主方法
<3> 方法的主体

这是你可以从Java中找到的典型代码，其中代码**必须**嵌入到可执行的类中。

.Main.groovy
[source,groovy]
----
println 'Groovy world!'
----

脚本可以被视为一个类而不需要声明它，但有一些差异。

=== 脚本类

{groovyJavaDocBaseUrl}?groovy/lang/Script.html[脚本] 始终编译为类。Groovy编译器将为你编译该类，并将脚本的主体复制到 `run` 方法中。因此，前面的示例被编译为如下所示：

.Main.groovy
[source,groovy]
----
import org.codehaus.groovy.runtime.InvokerHelper
class Main extends Script {                     // <1>
    def run() {                                 // <2>
        println 'Groovy world!'                 // <3>
    }
    static void main(String[] args) {           // <4>
        InvokerHelper.runScript(Main, args)     // <5>
    }
}
----
<1> `Main` 类继承自 `groovy.lang.Script` 类
<2> `groovy.lang.Script` 需要一个返回值的 `run` 方法
<3> 脚本主体写在 `run` 方法内
<4> `main` 方法是自动生成的
<5> 并在 `run` 方法上委派脚本的执行

如果脚本位于文件中，则使用该文件的基本名称来确定生成的脚本类的名称。在此示例中，如果文件的名称为 `Main.groovy`，则脚本类将为 `Main`。

=== 方法

可以在脚本中定义方法，如下所示：

[source,groovy]
----
int fib(int n) {
    n < 2 ? 1 : fib(n-1) + fib(n-2)
}
assert fib(10)==89
----

你还可以混合方法和代码。生成的脚本类将所有方法都包含在脚本类中，并将所有脚本体组装到 `run` 方法中：

[source,groovy]
----
println 'Hello'                                 // <1>

int power(int n) { 2**n }                       // <2>

println "2^6==${power(6)}"                      // <3>
----
<1> 脚本开始
<2> 在脚本体中定义了一个方法
<3> 和脚本继续

此代码在内部转换为：

[source,groovy]
----
import org.codehaus.groovy.runtime.InvokerHelper
class Main extends Script {
    int power(int n) { 2** n}                   // <1>
    def run() {
        println 'Hello'                         // <2>
        println "2^6==${power(6)}"              // <3>
    }
    static void main(String[] args) {
        InvokerHelper.runScript(Main, args)
    }
}
----
<1> 将 `power` 方法原样复制到生成的脚本类中
<2> 第一个语句被复制到 `run` 方法中
<3> 第二个语句被复制到 `run` 方法中

即使Groovy从你的脚本创建了一个类，它对用户来说也是完全透明的。特别是，脚本被编译为字节码，并保留行号。这意味着如果在脚本中抛出异常，堆栈跟踪将显示与原始脚本对应的行号，而不是我们显示的生成代码。

=== 变量

脚本中的变量不需要类型定义。这意味着这个脚本：

[source,groovy]
----
int x = 1
int y = 2
assert x+y == 3
----

将表现如下：

[source,groovy]
----
x = 1
y = 2
assert x+y == 3
----

但是两者之间存在语义差异：

* 如果变量在第一个示例中声明，则它是一个__局部变量__。它将在 `run` 方法中声明，编译器将生成并且在脚本主体外部**不可见**。特别是，这样的变量在脚本的其他方法中是**不可见**的。
* 如果变量未声明，则进入 {groovyJavaDocBaseUrl}?groovy/lang/Script.html#getBinding()[脚本绑定]。从方法中可以看到绑定，如果你使用脚本与应用程序交互并需要在脚本和应用程序之间共享数据，则这一点尤其重要。读者可参阅 {groovyITGuideDocBaseUrl}#_integrating_groovy_in_a_java_application[集成指南]以获取更多信息。

TIP: http://www.groovy-lang.org/structure.html[原文链接]
